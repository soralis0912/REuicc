plugins {
    id 'com.android.application'
}

android {
    namespace 'org.soralis_0912.reuicc_fix'
    compileSdk 33

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName "1.2"
    }

    signingConfigs {
        android {
            storeFile file('android.jks')
            storePassword 'android'
            keyAlias 'android'
            keyPassword 'android'
        }
        release {
            storeFile file('release.jks')
            storePassword System.getenv('STORE_PASSWORD')
            keyAlias System.getenv('KEY_ALIAS')
            keyPassword System.getenv('KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            multiDexEnabled false
            proguardFiles += 'proguard-rules.pro'
            signingConfig signingConfigs.android
        }
        release {
            minifyEnabled false
            multiDexEnabled false
            proguardFiles += 'proguard-rules.pro'
            if (file('release.jks').exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    compileOnly 'de.robv.android.xposed:api:82'

    def libsuVersion = '5.2.2'
    // The core module that provides APIs to a shell
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    // Optional: APIs for creating root services. Depends on ":core"
    implementation "com.github.topjohnwu.libsu:service:${libsuVersion}"
    // Optional: Provides remote file system support
    implementation "com.github.topjohnwu.libsu:nio:${libsuVersion}"
}
